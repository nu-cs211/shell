;; CS 211 Emacs configuration

;;
;; Backup files
;;

; Put backup files in ~/.saves instead of littering up
; everywhere else.
(setq backup-directory-alist    '(("." . "~/.saves"))
      backup-by-copying         t
      delete-old-versions       t
      kept-new-versions         5
      kept-old-versions         2
      version-control           t)


;;
;; C indentation
;;

; Our C indentation style
(defconst cs211-c-style
  '("k&r"
    (c-tab-always-indent . t)
    (c-cleanup-list      . (brace-else-brace
                            brace-elseif-brace
                            brace-catch-brace
                            defun-close-semi
                            empty-defun-braces
                            list-close-comma
                            scope-operator))
    (c-offsets-alist     . ((arglist-close . c-lineup-arglist))))
  "CS211 C(++) Style")
(c-add-style "cs211" cs211-c-style)

; In C mode, enable our style and some input settings.
(defun cs211-c-mode-common-hook ()
  (c-set-style "cs211")
  (setq c-basic-offset    4      ; indent by 4
        tab-width         8      ; tab characters are worth 8
        indent-tabs-mode  nil))  ; but don't use tab characters
(add-hook 'c-mode-common-hook 'cs211-c-mode-common-hook)

; In C mode, Enter key also indents.
(defun cs211-c-mode-init ()
  (define-key c-mode-base-map "\C-m" 'c-context-line-break))
(add-hook 'c-initialization-hook 'cs211-c-mode-init)


;;
;; Allow colors in shell
;;

(add-to-list 'load-path
             (format "%s/share/emacs/site-lisp" (getenv "TOV_PUB")))

(require 'xterm-color)

(defun setup-xterm-color-for-comint ()
  ;; Disable font-locking in this buffer to improve performance
  (font-lock-mode -1)
  ;; Prevent font-locking from being re-enabled in this buffer
  (make-local-variable 'font-lock-function)
  (setq font-lock-function (lambda (_) nil))
  ;; Send output through xterm-color
  (add-hook 'comint-preoutput-filter-functions 'xterm-color-filter nil t))

(setq comint-output-filter-functions
      (remove 'ansi-color-process-output comint-output-filter-functions))

(add-hook 'shell-mode-hook 'setup-xterm-color-for-comint)

;;
;; Make the mouse work!
;;

; Enable clicking and selecting
(xterm-mouse-mode)

; Enable scrolling
(global-set-key [mouse-4] 'previous-line)
(global-set-key [mouse-5] 'next-line)

(defun other-window-up ()
  (interactive)
  (scroll-other-window -1))

(defun other-window-down ()
  (interactive)
  (scroll-other-window 1))

; Shift-scroll scrolls the other window
(global-set-key [S-mouse-4] 'other-window-up)
(global-set-key [S-mouse-5] 'other-window-down)


;;
;; GDB setup
;;

; Prevent editing the prompt.
(setq comint-prompt-read-only t)

; Make sure we see the code (not for gud-gdb).
(setq gdb-show-main t)

; Use the 6-window UI (not for gud-gdb).
(setq gdb-many-windows t)

; Finds a window matching `regexp`.
(defun find-window-matching (regexp &optional windows)
  (catch 'done
    (dolist (window (or windows (window-list)))
      (when (string-match-p regexp (format "%s" (window-buffer window)))
        (throw 'done window)))))

; Selects the first window matching `regexp`
(defun select-window-matching (regexp &optional quiet)
  (interactive)
  (let ((window (find-window-matching regexp)))
    (cond
      (window      (select-window window))
      ((not quiet) (message "No such window")))))

; Selects the GUD command window.
(defun select-gud-command-window (&optional quiet)
  (interactive)
  (select-window-matching "^[*]gud-.*[*]$" quiet))

; Selects the GUD I/O window.
(defun select-gud-io-window (&optional quiet)
  (interactive)
  (select-window-matching "^[*]input/output of .*[*]$" quiet))


; Sends EOF to the inferior process and displays "^D" in the buffer.
(defun send-eof-and-leave ()
  (interactive)
  (comint-send-eof)
  (select-gud-command-window t))

; Prevents input echo and makes C-d work.
(defun configure-gud-io-window ()
  ;; Don't echo input
  (setq comint-process-echoes t)
  ;; Make ^D work
  (local-set-key (kbd "C-d") 'send-eof-and-leave)
  ;; Make color work nicely here
  (setup-xterm-color-for-comint))

(add-hook 'gdb-inferior-io-mode-hook 'configure-gud-io-window)

; Makes this window dedicated and binds M-i to switch to the I/O window.
(defun configure-gud-control-window ()
  (set-window-dedicated-p (selected-window) t)
  (local-set-key (kbd "M-i") 'select-gud-io-window))

(advice-add 'gdb-setup-windows :after
            'configure-gud-control-window)

;;
;; Keybindings
;;

(global-set-key "\C-x\C-m" 'compile)


;; vim: se ft=lisp:
